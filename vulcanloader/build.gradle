/*
Copyright 2019 NCC Group
Copyright 2024 Jeff Dileo

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    https://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'



buildscript {
  repositories {
    //jcenter()
    gradlePluginPortal()
  }

  dependencies {
    //classpath 'com.github.jengelman.gradle.plugins:shadow:+'
    classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
  }
}

apply plugin: 'com.github.johnrengelman.shadow'

//name = 'vulcanloader'
group = 'trust.nccgroup'
version = '1.0.0'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
  mavenLocal()
  mavenCentral()
}

String bbver = new File(projectDir.getAbsolutePath() + "/../caldum/bbver.txt").getText('UTF-8').strip()

dependencies {
  // for tools.jar on jdk < 9
  if (project.hasProperty('no-tools')) {
    compileOnly fileTree(include: ['*.jar'], dir: 'tools')
  } else {
    compile fileTree(include: ['*.jar'], dir: 'tools')
  }

  compile group: 'net.bytebuddy', name: 'byte-buddy', version: bbver
  //compile group: 'net.bytebuddy', name: 'byte-buddy', version: '[1.9.6, 1.11)'
  //compile group: 'net.bytebuddy', name: 'byte-buddy -agent', version: '[1.9.6, 1.11)'

  // currently relies on ./gradlew from ../caldum for local maven cache
  // will eventually get caldum on central repo
  compile group: 'trust.nccgroup', name: 'caldum', version: '1.0.0'

  //compile project(':caldum')
}

sourceSets {
  main {
    java {
      srcDirs = ['./src']
    }

    resources {
      srcDirs = ['./res']
    }
  }
}

jar {
  manifest {
    attributes (
      "Manifest-Version": "1.0",
      "Can-Redefine-Classes": "true",
      "Can-Retransform-Classes": "true",
      "Can-Set-Native-Method-Prefix": "true",
      "Main-Class": "trust.nccgroup.vulcanloader.Main",
      "Premain-Class": "trust.nccgroup.vulcanloader.PreMain",
      "Agent-Class": "trust.nccgroup.vulcanloader.AgentMain"
    )
  }
}


shadowJar {
  archiveName = "vl.${extension}"

  // removes -all from the build
  classifier = ''
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
  from javadoc
}

// note: for local publish of fat jar: ./gradlew publishToMavenLocal
publishing {
  /*publications {
    shadow(MavenPublication) { publication ->
      project.shadow.component(publication)
    }
  }*/
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact sourceJar {
        classifier "sources"
      }

      artifact javadocJar {
        classifier = 'javadoc'
      }
    }
  }

  repositories {
    maven {
      url "https://127.0.0.1:1"
    }
  }
}

defaultTasks "publishToMavenLocal", "shadowJar"
